# SPDX-FileCopyrightText: Copyright (c) 2024-2025 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

# Copy native operator multithread application
add_custom_target(python_multithread ALL
  COMMAND ${CMAKE_COMMAND} -E copy "${CMAKE_CURRENT_SOURCE_DIR}/multithread.py" ${CMAKE_CURRENT_BINARY_DIR}
  DEPENDS "multithread.py"
  BYPRODUCTS "multithread.py"
)

# Testing
if(BUILD_TESTING)
  find_package(Python3 REQUIRED COMPONENTS Interpreter)

  # Test flow tracking and data logging
  # (test_pattern_validation.py is used for regex-based checks for data logging output)
  # (make sure --num_delay_ops, --delay and --delay_step remain identical across the following
  #  cases involved in timing comparisons):
  #    - EXAMPLE_PYTHON_MULTITHREAD_TEST   (runs with all available threads)
  #    - EXAMPLE_PYTHON_MULTITHREAD_4_TEST (runs with 4 worker threads)
  #    - EXAMPLE_PYTHON_MULTITHREAD_1_TEST (runs with 1 worker threads)
  #    - EXAMPLE_PYTHON_MULTITHREAD_0_TEST (runs single-threaded GreedyScheduler)
  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_TEST
           COMMAND bash -c
             "${Python3_EXECUTABLE} multithread.py --threads -1 --num_delay_ops 8 --delay 0.25 \
               --delay_step 0.1 --data_log --name EXAMPLE_PYTHON_MULTITHREAD_TEST \
               --output_file multithread_times 2>&1 | \
             ${Python3_EXECUTABLE} ${CMAKE_SOURCE_DIR}/../bin/test_pattern_validation.py --strip-quotes \
               --pass-pattern 'sum of received values: 28' \
               --pass-pattern 'BasicConsoleLogger\\[ID:tx\.out\\]' \
               --pass-pattern 'BasicConsoleLogger\\[ID:delay07\.in\\]' \
               --pass-pattern 'BasicConsoleLogger\\[ID:delay07\.out_name\\]' \
               --pass-pattern 'BasicConsoleLogger\\[ID:rx\.values:7\\]' \
               --pass-pattern 'Category:Message \\(std::any\\)' \
               --pass-pattern 'Python\\(int\\): 7' \
               --pass-pattern 'Python\\(str\\): delay07' \
               --fail-pattern 'BasicConsoleLogger\\[ID:rx\\.names:7\\]' \
               --fail-pattern 'BasicConsoleLogger\\[ID:delay07\.out_val\\]'"
           WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )

  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_4_TEST
           COMMAND ${Python3_EXECUTABLE} multithread.py --threads 4 --num_delay_ops 8 --delay 0.25 --delay_step 0.1 --data_log
                   --name EXAMPLE_PYTHON_MULTITHREAD_4_TEST --output_file multithread_times
           WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )
  set_tests_properties(EXAMPLE_PYTHON_MULTITHREAD_4_TEST PROPERTIES
                       PASS_REGULAR_EXPRESSION "sum of received values: 28"
                       DEPENDS EXAMPLE_PYTHON_MULTITHREAD_TEST)

  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_1_TEST
           COMMAND ${Python3_EXECUTABLE} multithread.py --threads 1 --num_delay_ops 8 --delay 0.25 --delay_step 0.1 --data_log
           --name EXAMPLE_PYTHON_MULTITHREAD_1_TEST --output_file multithread_times
           WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )
  set_tests_properties(EXAMPLE_PYTHON_MULTITHREAD_1_TEST PROPERTIES
                       PASS_REGULAR_EXPRESSION "sum of received values: 28"
                       DEPENDS EXAMPLE_PYTHON_MULTITHREAD_4_TEST)

  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_0_TEST
           COMMAND ${Python3_EXECUTABLE} multithread.py --threads 0 --num_delay_ops 8 --delay 0.25 --delay_step 0.1 --data_log
           --name EXAMPLE_PYTHON_MULTITHREAD_0_TEST --output_file multithread_times
           WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})
  set_tests_properties(EXAMPLE_PYTHON_MULTITHREAD_0_TEST PROPERTIES
                       PASS_REGULAR_EXPRESSION "sum of received values: 28"
                       DEPENDS EXAMPLE_PYTHON_MULTITHREAD_1_TEST)

  # Add a test to check the validity of the timing
  # This is only valid on dgpu
  execute_process(COMMAND nvidia-smi -L
                  OUTPUT_VARIABLE HOST_GPU)
  if(NOT HOST_GPU MATCHES "nvgpu")
    add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_TIMES_VALIDATION_TEST
            COMMAND ${Python3_EXECUTABLE} ${CMAKE_SOURCE_DIR}/../bin/ctest_time_comparison.py
                  multithread_times
                  EXAMPLE_PYTHON_MULTITHREAD_TEST LESS EXAMPLE_PYTHON_MULTITHREAD_4_TEST
                  EXAMPLE_PYTHON_MULTITHREAD_4_TEST LESS EXAMPLE_PYTHON_MULTITHREAD_1_TEST
                  EXAMPLE_PYTHON_MULTITHREAD_4_TEST LESS EXAMPLE_PYTHON_MULTITHREAD_0_TEST
            WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
    )

    set_tests_properties(EXAMPLE_PYTHON_MULTITHREAD_TIMES_VALIDATION_TEST PROPERTIES
      DEPENDS "EXAMPLE_PYTHON_MULTITHREAD_TEST;EXAMPLE_PYTHON_MULTITHREAD_4_TEST;EXAMPLE_PYTHON_MULTITHREAD_1_TEST;EXAMPLE_PYTHON_MULTITHREAD_0_TEST"
      PASS_REGULAR_EXPRESSION "Timing for tests matches expectations"
    )
  endif()

  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_EVENT_BASED_TEST
    COMMAND ${Python3_EXECUTABLE} multithread.py --event_based
    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )
  set_tests_properties(EXAMPLE_PYTHON_MULTITHREAD_EVENT_BASED_TEST PROPERTIES
                       PASS_REGULAR_EXPRESSION "sum of received values: 496")

  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_TRACK_TEST
    COMMAND bash -c
      "${Python3_EXECUTABLE} multithread.py --track --count 5 2>&1 | \
      ${Python3_EXECUTABLE} ${CMAKE_SOURCE_DIR}/../bin/test_pattern_validation.py \
        --pass-pattern 'Number of messages: 6' \
        --pass-pattern 'tx->out: 5'"
    WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  )

  add_test(NAME EXAMPLE_PYTHON_MULTITHREAD_PRINT_PORT_MAP_TEST
          COMMAND ${Python3_EXECUTABLE} multithread.py -n 4 --print_port_map
          WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
  )
  # test a string expected to be present in the port map
  set_tests_properties(EXAMPLE_PYTHON_MULTITHREAD_PRINT_PORT_MAP_TEST PROPERTIES
                        PASS_REGULAR_EXPRESSION "output_to_input:"
  )
endif()
